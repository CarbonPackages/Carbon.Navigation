prototype(Carbon.Navigation:Link) < prototype(Neos.Fusion:Component) {
    // THIS PROTOTYPE IS FOR INTERNAL USE ONLY.
    // Properties can change without releasing a new major version.
    // You have to be aware of this if you use this prototype directly

    item = null
    wrapText = null
    namespace = null
    renderClass = null
    name = null
    overwriteIsFirst = null
    overwriteIsLast = null

    renderer = Carbon.Link:Link {
        prototype(Carbon.Link:Class) {
            prepend = ${props.namespace + '__link-'}
            linkType.@if.renderTypeClass = ${props.renderClass.link.type}
        }
        node = ${props.item.node}
        backendLink = true
        renderDefaultTagIfNoLink = true
        additionalClass = Carbon.Navigation:Class {
            @apply.props = ${props}
        }
        attributes.href = Neos.Neos:NodeUri {
            node = ${props.item.node}
        }
        content = Carbon.Navigation:Label {
            @apply.props = ${props}
            @process.wrap = Neos.Fusion:Tag {
                @if.wrap = ${props.wrapText}
                tagName = ${Type.isString(props.wrapText) ? props.wrapText : 'span'}
                content = ${value}
            }
        }
    }
}
